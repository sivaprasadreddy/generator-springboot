apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: "com.github.spotbugs"
apply plugin: "org.sonarqube"

def jacocoMinCoverageRequired = 0.30

checkstyle {
    toolVersion "8.32"
    maxWarnings = 0
    configFile = file("${project.rootDir}/build-config/checkstyle/checkstyle.xml")
    sourceSets = [sourceSets.main]
}

pmd {
    toolVersion = "6.23.0"
    consoleOutput = true
    ruleSets = []
    ruleSetFiles = files("${project.rootDir}/build-config/pmd/pmd-ruleset.xml")
    sourceSets = [sourceSets.main]
}

tasks.matching {task -> task.name.startsWith('spotbugs')}.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}

jacoco {
    toolVersion = "0.8.5"
}

def jacocoExcludes = [
    '**/*Application.*',
    '**/models/**',
    '**/exceptions/**',
    '**/dtos/**',
]

jacocoTestReport {
    executionData tasks.withType(Test)
    classDirectories.from = files(sourceSets.main.output.classesDirs)
    sourceDirectories.from = files(sourceSets.main.java.srcDirs)

    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        }))
    }

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")

    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = jacocoMinCoverageRequired
            }
            excludes = jacocoExcludes
        }
    }
}

integrationTest.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

file("sonar-project.properties").withReader {
    Properties sonarProperties = new Properties()
    sonarProperties.load(it)

    sonarProperties.each { key, value ->
        sonarqube {
            properties {
                property key, value
            }
        }
    }
}
